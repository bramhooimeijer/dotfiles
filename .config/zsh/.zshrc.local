export PIP_REQUIRE_VIRTUALENV=true
export PATH=/home/braho/.local/bin:$PATH
fpath=($ZDOTDIR/.zfunc/ $fpath)

alias graphviz='dot -Tx11'
# if nvim is installed, use it as alias for vim
if command -v nvim &> /dev/null; then
    alias vim='nvim'
    export EDITOR=nvim
    export VISUAL=nvim
else
    export EDITOR=vim
    export VISUAL=vim
fi
alias lh='ls -hAl --color=auto -v --group-directories-first'

# Set keymapping to VI-mode in zsh
bindkey -v
setopt vi
bindkey "^?" backward-delete-char #Fixes backspace

# Define the token and register it with grml
function virtual_env_prompt () {
    REPLY=${VIRTUAL_ENV+(${VIRTUAL_ENV:t}) }
}
grml_theme_add_token virtual-env -f virtual_env_prompt '%F{magenta}' '%f'

# Make the prompt use the token
zstyle ':prompt:grml:left:setup' items rc change-root user at host path virtual-env vcs percent

# Create a function to detect cmd mode for vi
function zle-line-init zle-keymap-select {
    VIM_PROMPT="[% NORMAL]%"
    RPS1="${${KEYMAP/vicmd/$VIM_PROMPT}/(main|viins)/} $EPS1"
    zle reset-prompt
}
zle -N zle-line-init
zle -N zle-keymap-select

function activate_venv {
    local folder_name="${1:-.venv}"
    local current_dir="$PWD"

    while true; do
      if [[ -d "$current_dir/$folder_name" && -f "$current_dir/$folder_name/pyvenv.cfg" ]]; then
        source "$current_dir/$folder_name/bin/activate"
        echo "Activated virtual environment in $current_dir/$folder_name"
        break
      else
        if [[ "$current_dir" == "/" ]]; then
          echo "Reached / without finding $folder_name"
          echo "Try to activate virtual environment in /home/braho/.scripts/.venv"
          source "$HOME/.scripts/$folder_name/bin/activate"
          break
        fi
        current_dir=$(dirname "$current_dir")
      fi
    done
}

# if .zshrc.local exists, source it. Use $ZDOTDIR if set, else $HOME
if [[ -f "${ZDOTDIR:-$HOME}/.zshrc.local2" ]]; then
    source "${ZDOTDIR:-$HOME}/.zshrc.local2"
fi


alias config='/usr/bin/git --git-dir=$HOME/.config/.dotgit --work-tree=$HOME'

# Start sway on tty1
if command -v sway &> /dev/null && [ "$(tty)" = "/dev/tty1" ]; then
	XDG_CURRENT_DESKTOP=sway WLR_DRM_NO_ATOMIC=1 exec sway > /var/log/sway.log 2>&1
fi
